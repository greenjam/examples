
BRANCH_VERSION = 1.0
version = getBuildVersion()

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'jacoco'
    apply plugin: 'project-report'

    sourceCompatibility = '1.7'

    repositories {
        mavenCentral()
    }
}

subprojects.each {
    it.tasks.withType(JavaCompile){
        options.deprecation = true
        options.compilerArgs =['-Xlint:none','-Xlint:unchecked']
    }

    it.tasks.withType(Test) {
        jacoco {
            enabled = project.hasProperty("includeCoverage")
        }

        minHeapSize = "2G"
        maxHeapSize = "2G"

        jvmArgs = [
                '-XX:-UseSplitVerifier',
                '-XX:+CMSClassUnloadingEnabled',
                '-XX:+HeapDumpOnOutOfMemoryError',
                '-XX:MaxPermSize=768m',
                '-Dfile.encoding=utf-8'
        ]

        useTestNG {
            useDefaultListeners = true
        }

        testLogging {
            events 'failed', 'skipped'
        }
    }
}

task coverageReport(type: JacocoReport){
    group = "Reporting"
    descriptiom = "Generate coverage reports after tests."
    sourceSets project(":hello-world-api").sourceSets.main
    executionData fileTree(project.rootDir.absolutePath).include("*/build/jacoco/*.exec")
    reports {
        xml.enabled true
        html.enabled true
    }
}

def getBuildVersion() {
    return String.valueOf(BRANCH_VERSION) + '.' + getBuildNumber()
}

def getBuildNumber() {
    return System.getenv("BUILD_NUMBER") == null ?
            "SNAPSHOT" :
            System.getenv("BUILD_NUMBER")
}
